*** ALKUHOMMAT ***************

* 1. avatkaa ESS7 data
* 2. ajakaa ESS_miss.do (tuli aineiston lataamisen mukana)

*** ladataan mahdolliset tarvittavat paketit
ssc install tabout // tabout
ssc install estout // estout

* taulukko-ohjeet
	* https://github.com/lausau/praktikum2017/blob/master/descriptives

* regressio-ohjeet
	* https://github.com/lausau/praktikum2017/blob/master/descriptives 
	
* painoista (ajakaa tama)
	gen paino = pweight*pspwght
	* https://www.europeansocialsurvey.org/docs/methodology/ESS_weighting_data_1.pdf

*******************************

*** ajakaa omat muuttujamuunnokset ***

*** KOULUTUS

	* tehty ISCED -asteikolla
	* suomalaisia vastineita taalla: http://www.tilastokeskus.fi/meta/luokitukset/_linkki/iscedaste11.html
	
	/* 
	ta eisced

	Highest level of education, ES - ISCED  |      Freq.     Percent        Cum.
	----------------------------------------+-----------------------------------
	ES-ISCED I , less than lower secondary  |        535        8.58        8.58
	ES-ISCED II, lower secondary		    |        801       12.85       21.43
	ES-ISCED IIIb, lower tier upper seconda |        816       13.09       34.52
	ES-ISCED IIIa, upper tier upper seconda |      1,171       18.78       53.30
	ES-ISCED IV, advanced vocational, sub-d |      1,049       16.83       70.13
	ES-ISCED V1, lower tertiary education,  |      1,033       16.57       86.70
	ES-ISCED V2, higher tertiary education, |        819       13.14       99.84
                                  O	   ther |         10        0.16      100.00
	----------------------------------------+-----------------------------------
									Total   |      6,234      100.00
	*/					
									
	* luokitellaan uusiksi kolmiluokkaiseksi (perus/keski/korkea -aste)
	gen koulutus = .
		replace koulutus = 1 if eisced < 3 // perusaste
		replace koulutus = 2 if eisced > 2 & eisced < 6 // keskiaste
		replace koulutus = 3 if eisced > 5 // korkea-aste
		replace koulutus = . if eisced == 55 // "other" puuttuvaksi arvoksi
		
	* labelointi
	label variable koulutus "koulutusaste"
	label define koulutuslab 1 "perusaste" 2 "keskiaste" 3 "korkea-aste"
	label values koulutus koulutuslab
		
	* tarkastellaan muuttujaa
	ta koulutus eisced
	
	/*
		ta koulutus eisced
		

	koulutusaste|                    Highest level of education, ES - ISCED
			    | ES-ISCED   ES-ISCED   ES-ISCED   ES-ISCED   ES-ISCED   ES-ISCED   ES-ISCED  |     Total
	------------+-----------------------------------------------------------------------------+----------
	perusaste   |       535        801          0          0          0          0          0 |     1,336 
	keskiaste   |         0          0        816      1,171      1,049          0          0 |     3,036 
	korkea-aste |         0          0          0          0          0      1,033        819 |     1,852 
	------------+-----------------------------------------------------------------------------+----------
		  Total |       535        801        816      1,171      1,049      1,033        819 |     6,224 
	*/


*** SIVIILISAATY

* Luokittelu Tilastokeskuksesta:
* http://tilastokeskus.fi/meta/luokitukset/siviilisaaty/001-2002/index.html
/* 
	ta maritalb

       Legal marital status, post coded |      Freq.     Percent        Cum.
----------------------------------------+-----------------------------------
                        Legally married |     19,863       49.43       49.43
    In a legally registered civil union |        346        0.86       50.29
                      Legally separated |        227        0.56       50.85
 Legally divorced/civil union dissolved |      4,050       10.08       60.93
             Widowed/civil partner died |      3,510        8.73       69.67
None of these (NEVER married or in lega |     11,777       29.31       98.97
                                Refusal |        109        0.27       99.25
                             Don't know |        137        0.34       99.59
                              No answer |        166        0.41      100.00
----------------------------------------+-----------------------------------
                                  Total |     40,185      100.00
*/					
							
	* luokitellaan uusiksi neli (perus/keski/korkea -aste)
	gen ss = .
		replace ss = 1 if maritalb == 6 // naimaton
		replace ss = 2 if maritalb == 1 | maritalb == 2 // avo- tai avioliitto
		replace ss = 3 if inlist(maritalb, 3, 4, 5, 77, 88, 99) // eronnut, leski tai muu

		
	* labelointi
	label variable ss "siviilisaaty"
	label define ssl 1 "naimaton" 2 "avo- tai avioliitto" 3 "muu"
	label values ss ssl
		
	* tarkastellaan muuttujaa
	ta maritalb ss
	
	
	
*** KUVAILEVIA ANALYYSEJA ***

* GRAAFI

* kuva jossa tyotunnit ja henkinen hyvinvointi
twoway (histogram henkhv if tyotunnit==1, color(green) width(1) percent) ///
       (histogram henkhv if tyotunnit==2, width(1) percent ///
	   fcolor(none) lcolor(black)), legend(order(1 "osa-aika" 2 "kokoaika" ))

	
	
*** ASKELTAVA REGRESSIOANALYYSI ***

* mark nomiss ja markout = maarittelevat muuttujat siten etta ottavat mukaan vain jos ei puuttuva arvo. Tama oltava regressiomallien vertailujen vuoksi.
* aw=paino = painottaa aineiston [paino laskettu taman do-filen alussa]

mark nomiss // tama ajetaan vain kerran
markout nomiss henkhv työtunnit gndr agea koulutus // laittakaa tahan kaikki muuttujat joita regressioanalyysissa kaytatte

* tehdaan ensimmainen malli
reg henkhv työtunnit if nomiss [aw=paino] 
margins, dydx(*) // tallennetaan marginaaliefektit
est sto malli1 // tallennetaan ensimmaisen mallin estimaatit

* toinen malli
reg henkhv työtunnit i.koulutus if nomiss [aw=paino]
margins, dydx(*)
est sto malli2

* kolmas malli
reg henkhv työtunnit gndr c.agea i.koulutus if nomiss [aw=paino]
margins, dydx(*)
est sto malli3

* tuodaan mallit yhteen ja tallennetaan nimella "regtable.rtf"
esttab malli1 malli2 malli3 using regtable.rtf, b(3) se(3) r2 nodepvars ///
	label mtitles("Malli 1" "Malli 2" "Malli 3") nonum compress ///
	replace title(Taulukko 1. tähän taulukon yläpuolelle tuleva teksti)

	* "nonum" poistaa numerot (1) (2) (3) -Mallitekstien päältä

*** HUOMIOITA
/* tehkaa maksimissaan nelja mallia.
	- Ensimmaisen mallin on hyva olla "tyhja", jotta nahdaan miten se kaikkein
	mielenkiintoisin selittava tekija toimii.
	- toiseen malliin voidaan lisata esimerkiksi toiseksi mielenkiintoisin tekija (edu),
	jos ollaan kiinnostuneita sen (paa)vaikutuksista.
	- Lopuksi voidaan lisata muita selittavia tekijoita (joskus nama kaksi viimeista mallia tehdaan toisinpain). 
	viimeisessa mallissa on joka tapauksessa oltava kaikki selittavat tekijat 
*/
